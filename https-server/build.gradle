plugins {
    id 'org.springframework.boot' version '2.7.2'
    id 'io.spring.dependency-management' version '1.0.12.RELEASE'
    id 'java'
    id 'nebula.integtest' version '9.6.2'
    id "com.dorongold.task-tree" version "2.1.0"
}

group = 'ru.alfabank.skillbox.examples'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

bootRun {
    jvmArgs = ["-Djavax.net.ssl.trustStore=$path_to_server_keystore/localhost.server.keystore",
               "-Djavax.net.ssl.trustStorePassword=$server_keystore_password",
               "-Djavax.net.ssl.trustStoreType=JKS",
               "-Djavax.net.ssl.keyStore=$path_to_server_keystore/localhost.server.keystore",
               "-Djavax.net.ssl.keyStorePassword=$server_keystore_password",
               "-Djavax.net.ssl.keyStoreType=JKS",
               "-Djavax.net.debug=all"]
}

repositories {
    mavenCentral()
}

ext {
    set('springCloudVersion', "2021.0.3")
    set('testcontainersVersion', "1.17.3")
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation "org.apache.commons:commons-lang3:3.12.0"
    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'org.postgresql:postgresql'
    implementation 'com.vladmihalcea:hibernate-types-52:2.3.4'

    annotationProcessor 'org.projectlombok:lombok'

    // Unit test own dependencies
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.testcontainers:junit-jupiter'
    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'

    // integration test own dependencies
    integTestImplementation 'org.springframework.cloud:spring-cloud-contract-wiremock:2.1.3.RELEASE'
    integTestImplementation 'org.testcontainers:postgresql'
    integTestImplementation 'com.github.dasniko:testcontainers-keycloak:2.2.2'
    integTestRuntimeOnly 'com.h2database:h2'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        mavenBom "org.testcontainers:testcontainers-bom:${testcontainersVersion}"
    }
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.named('integrationTest') {
    useJUnitPlatform()
}